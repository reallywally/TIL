# 자바8, 스프링부트2.7, QueryDSL

## 상황

고객사 서버 환경에 맞춰서 스프링 환경을 구성하려 한다. 그런데 고객 서버 환경이 조금 클래식하다보니 원하는 스펙으로 버전을 맞추는게 참 어려웠다. 금융권 특성상 이런 경우가 많기도 할것 같아서 정리해본다.

### 상세 스펙

- 자바8
- 스프링 부트 2.7.18
- JPA
- QueryDSL

## gradle

```gradle
plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.company.server'
version = '0.0.1-SNAPSHOT'
description = 'Classic for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// Querydsl 버전 명시적 관리
ext {
    querydslVersion = '4.4.0'
}

dependencies {
    // Spring Boot Starter
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    
    // Querydsl
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}"
    implementation "com.querydsl:querydsl-core:${querydslVersion}"
    implementation "com.querydsl:querydsl-codegen:${querydslVersion}"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jpa"
    annotationProcessor "com.querydsl:querydsl-codegen:${querydslVersion}"
    annotationProcessor "javax.annotation:javax.annotation-api:1.3.2"
    annotationProcessor "javax.persistence:javax.persistence-api:2.2"
    
    // Oracle JDBC Driver (19c). DB에 따라 다르게 사용 
    implementation 'com.oracle.database.jdbc:ojdbc8:19.3.0.0'
    
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// Querydsl 설정부분
sourceSets {
    main {
        java {
            srcDir 'build/generated' // QClass가 생성될 디렉터리
        }
    }
}

tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file("build/generated"))
    options.encoding = "UTF-8"
}

tasks.named('test') {
    useJUnitPlatform()
}
```

## 메모

- 스프링 부트 2.7 / Gradle 7 이상에서는 querydsl 플러그인(ewerk)을  쓰지 않는 게 안정적
- QueryDSL 5.x 는 Java 8 완벽 지원하지 않아 4.4.0으로 맞춤
- 최신버전의 인텔리제이도 빌드 완료하고 Q클래스 파일이 없어서 껏다 켜보니 되더라
- Spring Initializr도 이제 최저 자바가 17, 스프링 부트도 3.4.12더라
